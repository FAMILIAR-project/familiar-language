:author = Paul;
:version = 2.6 K&R Style;
:date = 10/21/2009;
:enum cpuSocket :in {
	LGA1156, 
	ASB1
};
:enum screenConnectivity :in {
	VGA, 
	DVI
};
:struct motherBoardSize { 
	:int height;  
	:int width;
};
:root Computer
{
	:int price
		:sel :sum(:SelectedChildren.price);
	:sel MotherBoard.socket == CPU.socket;
	:gr[3,4] { 
		MotherBoard
		{
			:int price
	    		:sel :sum(:SelectedChildren.Price);
	    	cpuSocket socket;
	    	motherBoardSize size { 
	    		height
	      			:sel :sum(:SelectedChildren.size.height);
	      		width
	      			:sel :sum(:SelectedChildren.size.width);
	    	};	
	    	:gr[1,1] { 
	    		Asus
	    		{
	        		:int price 
	       				:sel 259;
	       			motherBoardSize size { 
	       				height
	      					:sel 30;
	      				width
	      			    	:sel 40;
	      			};
	       			:sel :Parent.socket == LGA1156; 
	       		}
	       		Aopen
	       		{
	       			:int price
	       				:sel 199;
	       			motherBoardSize size { 
	       				height
	      					:sel 30;
	      				width
	      			  		:sel 40;
	      			};	
	       			:sel :Parent.socket == ASB1; 
	       		}
	    	}
		} 
		CPU
		{
			:int price 
	    		:sel :sum(:SelectedChildren.price);
	    	cpuSocket socket;
	    	:gr[1,1] { 
	    		CoreI7
	    		{
	      			:int price
	      				:sel 399;
	      			:sel :Parent.socket == LGA1156; 
	    		}
      			AthlonNeo
      			{
	      			:int price
	      				:sel 299;	
	      			:sel :Parent.socket == ASB1; 
	       		}  		
	    	}
		}
		GraphicCard
		{
			:int price
	    		:sel :sum(:SelectedChildren.price);
	    	screenConnectivity connectivity;
	    	:gr[1,1] {
	    		NVIDIA
	    		{
	      			:int price
	      				:sel 199;		
	      			:sel :Parent.connectivity == DVI; 
	       		}
      			ATI
      			{
	      			:int price
	      				:sel 159;		
	      			:sel :Parent.connectivity == VGA; 
	       		}
	    	}
		} 		
		:opt Accesories
		{
			:int price
				:sel :sum(:SelectedChildren.price);
			:sel !(PhilipsScreen && SamsungScreen);
			:gr[1,2] { 
				:opt LogitechKeyboardAndMouse
				{
					:int price
						:sel 19;
				}
				:opt PhilipsScreen
				{
					:int price
						:sel 99;	  	  
					:sel GraphicCard.connectivity == VGA;
				}
				:opt SamsungScreen
				{
					:int price
						:sel 149;	  
					:sel GraphicCard.connectivity == DVI;
				}
			}
		}
	}
}