// The main model

root SkodaConfig group allof {
	Model,
	Motor,
	Color,
	Option
}

// Models

Model group oneof {
	Fabia_Hatch group oneof {
		Fabia_Hatch_S,
		Fabia_Hatch_GreenLine_II,
		Fabia_Hatch_SE,
		Fabia_Hatch_SE_Plus,
		Fabia_Hatch_Elegance,
		Fabia_Hatch_Monte_Carlo,
		Fabia_Hatch_vRS
	},
	Fabia_Estate group oneof {
		// XXX not explored
		Fabia_Estate_S,
		Fabia_Estate_GreenLine_II,
		Fabia_Estate_SE,
		Fabia_Estate_SE_Plus,
		Fabia_Estate_Elegance,
		Fabia_Estate_Scout,
		Fabia_Estate_vRS
	},
	Roomster group oneof {
		// XXX not explored
		Roomster_S,
		Roomster_GreenLine_II,
		Roomster_SE,
		Roomster_SE_Plus,
		Roomster_Scout
	},
	Yeti group oneof {
		Yeti_E,
		Yeti_S,
		Yeti_SE, // XXX not explored
		Yeti_SE_Plus,
		Yeti_Elegance,
		Yeti_Urban
	},
	Octavia_Hatch group oneof {
		// XXX not explored
		Octavia_Hatch_S,
		Octavia_Hatch_Greenline,
		Octavia_Hatch_SE,
		Octavia_Hatch_SE_Plus,
		Octavia_Hatch_Elegance,
		Octavia_Hatch_LK,
		Octavia_Hatch_vRS
	},
	Octavia_Estate group oneof {
		// XXX not explored
		Octavia_Estate_S,
		Octavia_Estate_GreenLine_II,
		Octavia_Estate_SE,
		Octavia_Estate_SE_Plus,
		Octavia_Estate_Elegance,
		Octavia_Estate_LK,
		Octavia_Estate_4x4,
		Octavia_Estate_Scout,
		Octavia_Estate_vRS
	},
	Suberb_Hatch group oneof {
		// XXX not explored
		Superb_Hatch_S,
		Superb_Hatch_SE,
		Superb_Hatch_SE_Plus,
		Superb_Hatch_Elegance
	},
	Superb_Estate group oneof {
		Superb_Estate_S,
		Superb_Estate_SE,
		Superb_Estate_SE_Plus,
		Superb_Estate_Elegance
	}
}

// Motors

Motor group oneof {

	M_12_12V_60PS_5spd { ifin: or(Fabia_Hatch_S); },
	M_12_12V_69PS_5spd { ifin: or(Fabia_Hatch_S, Fabia_Hatch_SE, Fabia_Hatch_SE_Plus, Fabia_Hatch_Elegance, Fabia_Hatch_Monte_Carlo, Fabia_Estate_S, Fabia_Estate_SE, Fabia_Estate_SE_Plus, Fabia_Estate_Elegance, Roomster_S, Roomster_SE_Plus); },

	M_12_TSI_86PS_5spd { ifin: or(Fabia_Hatch_SE, Fabia_Hatch_SE_Plus, Fabia_Hatch_Elegance, Fabia_Hatch_Monte_Carlo, Fabia_Estate_SE, Fabia_Estate_SE_Plus, Fabia_Estate_Elegance, Fabia_Estate_Scout, Roomster_S, Roomster_SE, Roomster_SE_Plus, Roomster_Scout); },
	M_12_TSI_105PS { ifin: or(Yeti_E, Yeti_S, Yeti_SE, Yeti_SE_Plus, Yeti_Elegance, Yeti_Urban, Octavia_Hatch_S, Octavia_Hatch_SE); },
	M_12_TSI_105PS_DSG { ifin: or(Yeti_S, Yeti_Elegance, Octavia_Hatch_S, Octavia_Hatch_SE); },
	M_12_TSI_105PS_5spd { ifin: or(Fabia_Hatch_SE, Fabia_Hatch_SE_Plus, Fabia_Hatch_Elegance, Fabia_Hatch_Monte_Carlo, Fabia_Estate_SE, Fabia_Estate_SE_Plus, Fabia_Estate_Elegance, Roomster_SE, Roomster_SE_Plus, Roomster_Scout); },
	M_12_TSI_105PS_7spd_DSG { ifin: or(Fabia_Hatch_S, Fabia_Hatch_SE, Fabia_Hatch_SE_Plus, Fabia_Hatch_Elegance, Fabia_Estate_S, Fabia_Estate_SE, Fabia_Estate_SE_Plus, Fabia_Estate_Elegance, Roomster_S, Roomster_SE, Roomster_SE_Plus, Roomster_Scout); },

	M_14_16V_80PS { ifin: or(Octavia_Hatch_S); },
	M_14_TSI_120PS { ifin: or(Yeti_S, Yeti_SE, Yeti_SE_Plus, Yeti_Elegance); },
	M_14_TSI_122PS { ifin: or(Octavia_Hatch_S, Octavia_Hatch_SE, Octavia_Hatch_SE_Plus, Octavia_Hatch_Elegance); },
	M_14_TSI_122PS_DSG { ifin: or(Octavia_Hatch_S, Octavia_Hatch_SE, Octavia_Hatch_SE_Plus, Octavia_Hatch_Elegance); },
	M_14_TSI_180PS_7spd_DSG { ifin: or(Fabia_Hatch_vRS, Fabia_Estate_vRS); },

	M_16_MPI_102PS_5spd_Man { ifin: or(Octavia_Hatch_SE, Octavia_Hatch_Elegance); },

	M_16_TDI_CR_105PS_DPF { ifin: or(Octavia_Hatch_S, Octavia_Hatch_SE, Octavia_Hatch_SE_Plus, Octavia_Hatch_Elegance); },
	M_16_TDI_CR_105PS_DSG_DPF { ifin: or(Octavia_Hatch_S, Octavia_Hatch_SE, Octavia_Hatch_SE_Plus, Octavia_Hatch_Elegance); },

	M_16_TDI_CR_90PS_5spd { ifin: or(Roomster_SE, Roomster_SE_Plus, Roomster_Scout); },
	M_16_TDI_CR_105PS_5spd { ifin: or(Roomster_SE, Roomster_SE_Plus, Roomster_Scout); },

	M_16_TDI_CR_75PS_5spd_DPF { ifin: or(Fabia_Hatch_S, Fabia_Hatch_SE, Fabia_Hatch_SE_Plus, Fabia_Hatch_Elegance, Fabia_Hatch_Monte_Carlo, Fabia_Estate_S, Fabia_Estate_SE, Fabia_Estate_SE_Plus, Fabia_Estate_Elegance, Fabia_Estate_Scout); },
	M_16_TDI_CR_90PS_5spd_DPF { ifin: or(Fabia_Hatch_S, Fabia_Hatch_SE, Fabia_Hatch_SE_Plus, Fabia_Hatch_Elegance, Fabia_Estate_S, Fabia_Estate_SE, Fabia_Estate_SE_Plus, Fabia_Estate_Elegance, Fabia_Estate_Scout); },
	M_16_TDI_CR_105PS_5spd_DPF { ifin: or(Fabia_Hatch_SE, Fabia_Hatch_SE_Plus, Fabia_Hatch_Elegance, Fabia_Hatch_Monte_Carlo, Fabia_Estate_SE, Fabia_Estate_SE_Plus, Fabia_Estate_Elegance, Fabia_Estate_Scout); },

	M_18_TSI_152_PS_6G_Man {ifin: or(Octavia_Hatch_SE_Plus); },
	M_18_TSI_152_PS_4x4 { ifin: or(Yeti_SE_Plus); },
	M_18_TSI_160PS_4x4 { ifin: or(Yeti_SE, Yeti_Elegance); },
	M_18_TSI_160PS { ifin: or(Octavia_Hatch_Elegance, Octavia_Hatch_LK); },
	M_18_TSI_160PS_DSG { ifin: or(Octavia_Hatch_Elegance, Octavia_Hatch_LK); },

	M_20_TDI_CR_DPF_110_PS_6G_Man { ifin: or(Octavia_Hatch_SE_Plus); },

	M_20_TDI_CR_110PS_DPF { ifin: or(Yeti_E, Yeti_S, Yeti_SE, Yeti_SE_Plus, Yeti_Elegance, Yeti_Urban); },
	M_20_TDI_CR_110PS_4x4_DPF { ifin: or(Yeti_S, Yeti_SE, Yeti_SE_Plus, Yeti_Elegance); },

	M_20_TDI_CR_140PS_DPF { ifin: or(Octavia_Hatch_SE, Octavia_Hatch_SE_Plus, Octavia_Hatch_Elegance, Octavia_Hatch_LK); },
	M_20_TDI_CR_140PS_DSG_DPF { ifin: or(Octavia_Hatch_Elegance, Octavia_Hatch_LK); },
	M_20_TDI_CR_140PS_4x4_DPF { ifin: or(Yeti_SE, Yeti_SE_Plus, Yeti_Elegance); },
	M_20_TDI_CR_140PS_4x4_DSG_DPF { ifin: or(Yeti_SE, Yeti_SE_Plus, Yeti_Elegance); },
	M_20_TDI_CR_170PS_DPF { ifin: or(Octavia_Hatch_vRS); },
	M_20_TDI_CR_170PS_DSG_DPF { ifin: or(Octavia_Hatch_vRS); },

	M_20_TDI_CR_170PS_4x4_DPF { ifin: or(Yeti_SE_Plus, Yeti_Elegance); },

	M_20_T_FSI_200PS { ifin: or(Octavia_Hatch_vRS); },
	M_20_T_FSI_200PS_DSG { ifin: or(Octavia_Hatch_vRS); },

	M_Greenline_II_12_TDI_CR_75PS_5spd_DPF { ifin: or(Fabia_Hatch_GreenLine_II, Fabia_Estate_GreenLine_II); },
	M_GreenLine_II_16_TDI_CR_DPF_105PS_DPF { ifin: or(Yeti_S, Yeti_SE, Yeti_Elegance, Octavia_Hatch_Greenline); },
	
	M_12_TDI_CR_DPF_75_PS_5G_Man { ifin: or(Roomster_GreenLine_II); }
}

// Colors

Color group allof {
	// XXX list individual colors
	ColorExt group oneof {
		Metallic_colors,
		Metallic_colors_for_silver_roof {
			ifin: or(Fabia_Hatch_SE, Fabia_Hatch_SE_Plus, Fabia_Hatch_Elegance);
		},
		Metallic_colors_for_black_roof {
			ifin: or(Fabia_Hatch_Monte_Carlo, Fabia_Hatch_vRS);
		},
		Metallic_colors_for_white_roof {
			ifin: or(Fabia_Hatch_vRS);
		},
		Solid_colors {
			ifin: ! or(Fabia_Hatch_SE_Plus, Fabia_Hatch_Monte_Carlo);
		},
		Solid_colors_for_silver_roof {
			ifin: or(Fabia_Hatch_SE, Fabia_Hatch_SE_Plus, Fabia_Hatch_Elegance);
		},
		Solid_colors_for_black_roof {
			ifin: or(Fabia_Hatch_Monte_Carlo, Fabia_Hatch_vRS);
		},
		Solid_colors_for_white_roof {
			ifin: or(Fabia_Hatch_vRS);
		},
		Special_colors {
			ifin: or(Fabia_Hatch && !Fabia_Hatch_Monte_Carlo);
		}
	},
	ColorInt group oneof {
		ColorInt_normal,
		ColorInt_special_SE {
			ifin: or(Fabia_Hatch_SE, Superb_Estate_SE);
		},
		ColorInt_special_L_K {
			ifin: or(Superb_Estate_Elegance);
		},
		ColorInt_MonteCarlo_red {
			ifin: or(Fabia_Hatch_Monte_Carlo);
		}
	}
}

// Options

Option group [0..*] {
	Opt10_Safety group [0..*] {
		Curtain_airbags {
			ifin: or(Fabia_Hatch && ! Fabia_Hatch_vRS);
		},
		Rear_side_airbags {
			ifin: or(Superb_Estate);
		},
		Stability group [1..1] {
			ABS_plus_TPM {
				ifin: or(Fabia_Hatch_SE);
				ifin: ! TPM;
			},
			ESP_plus_ASR {
				ifin: or(Fabia_Hatch_S, Fabia_Hatch_SE, Fabia_Hatch_SE_Plus, Fabia_Hatch_Elegance, Fabia_Hatch_Monte_Carlo);
			},
			ESP_TPM_HHC {
				ifin: or(Fabia_Hatch_SE, Fabia_Hatch_SE_Plus, Fabia_Hatch_Elegance, Fabia_Hatch_Monte_Carlo);
				ifin: ! TPM;
			},
			HHC_TPM {
				ifin: or(Superb_Estate_S, Superb_Estate_SE_Plus, Superb_Estate_Elegance);
				ifin: ! TPM;
			},
			ESP_ABS_EBV_MSR_ASR_EDS_MBA_DSR {
				ifin: or(Yeti_E, Yeti_S);
			}
		},
		Bi_xenon_headlight_with_AFS {
			ifin: or(Superb_Estate_SE, Superb_Estate_SE_Plus);
			ifin: ! Headlight_washers;
		},
		Fog_lights group [1..1] {
			Cornering_front_fog_lights {
				ifin: or(Fabia_Hatch_SE, Fabia_Hatch_SE_Plus, Fabia_Hatch_Elegance);
			},
			Front_fog_lights {
				ifin: or(Fabia_Hatch_S, Fabia_Hatch_SE, Fabia_Hatch_SE_Plus);
			},
			Curve_projector_halogen_headlights_including_cornering_front_fog_lights {
				ifin: or(Fabia_Hatch_Elegance);
			}
		},
		TPM {
			ifin: or(Fabia_Hatch_SE, Fabia_Hatch_SE_Plus, Fabia_Hatch_Elegance, Fabia_Hatch_Monte_Carlo,
				Superb_Estate_S, Superb_Estate_SE_Plus);
		}
	},
	Opt20_Functionality { group [0..*] {
		Glovebox_cover {
			ifin: or(Fabia_Hatch_S);
		},
		Glasses_storage_compartment {
			ifin: or(Fabia_Hatch_vRS);
		},
		Sports_suspension {
			ifin: or(Fabia_Hatch_S, Fabia_Hatch_SE, Fabia_Hatch_SE_Plus, Fabia_Hatch_Elegance);
		},
		Storage_box group [1..1] {
			Under_passenger_seat {
				ifin: or(Fabia_Hatch_S, Fabia_Hatch_SE, Fabia_Hatch_SE_Plus, Fabia_Hatch_Elegance, Fabia_Hatch_Monte_Carlo, Fabia_Hatch_vRS);
			},
			Under_driver_seat {
				ifin: or(Fabia_Hatch_SE, Fabia_Hatch_SE_Plus, Fabia_Hatch_Elegance, Fabia_Hatch_Monte_Carlo, Fabia_Hatch_vRS);
			},
			Front_Seats {
				ifin: or(Fabia_Hatch_S, Fabia_Hatch_SE, Fabia_Hatch_Elegance, Fabia_Hatch_Monte_Carlo, Fabia_Hatch_vRS);
			}
		},
		Tow_bar group [1..1] {
			Preparation_for_tow_bar {
				ifin: or(Fabia_Hatch_S, Fabia_Hatch_SE, Fabia_Hatch_SE_Plus, Fabia_Hatch_Elegance,
					Superb_Estate, Yeti_S);
			},
			Tow_bar_removable {
				ifin: or(Fabia_Hatch_S, Fabia_Hatch_SE, Fabia_Hatch_SE_Plus, Fabia_Hatch_Elegance);
			}
		},
		Roof_rails_eloxed {
			ifin: or(Superb_Estate_S);
		},
		Silver_roof_rails {
			ifin: or(Yeti_S);
		},
		Electrically_operated_tailgate {
			ifin: or(Superb_Estate);
		},
		Heated_windshield_washer_nozzles {
			ifin: or(Superb_Estate);
		},
		Net_programme {
			ifin: or(Superb_Estate, Yeti_S && Variable_boot_floor);
		},
		Retractable_parcel_shelf {
			ifin: or(Superb_Estate);
		},
		Mechanical_roller_sun_blinds_for_rear_side_windows {
			ifin: or(Superb_Estate);
		},
		Smoking_package {
			ifin: or(Superb_Estate);
		},
		Floor_mats group [1..1] {
			Textile_floor_mats {
				ifin: or(Superb_Estate);
			},
			L_K_front_and_rear_floor_mats {
				// already constrained
			}
		},
		Boot_floor group [1..1] {
			Boot_floor_with_integrated_aluminium_strips_and_fastening_elements {
				ifin: or(Superb_Estate_S);
			},
			Variable_boot_floor {
				ifin: or(Superb_Estate, Yeti_S && Net_programme);
			}
		},
		Rough_road_package {
			ifin: or(Yeti_S);
		},
		Textile_floor_mats {
			ifin: or(Yeti_S);
		}
	} // other constraints
		Electrically_operated_tailgate excludes Retractable_parcel_shelf;
	},
	Opt30_Interior_functionality group [0..*] {
		Steering_wheel { group [1..1] {
			Three_spoke_leather_steering_wheel_and_small_leather_pack {
				ifin: or(Fabia_Hatch_SE, Fabia_Hatch_Elegance);
			},
			Three_spoke_MFSW_for_radio_and_small_leather_pack {
				ifin: or(Fabia_Hatch_SE, Fabia_Hatch_Elegance, Fabia_Hatch_Monte_Carlo);
			},
			Three_spoke_MFSW_for_radio_and_telephone_and_small_leather_pack {
				ifin: or(Fabia_Hatch_SE, Fabia_Hatch_SE_Plus, Fabia_Hatch_Elegance, Fabia_Hatch_Monte_Carlo);
			},
			Three_spoke_multifunctional_leather_steering_wheel_for_radio {
				ifin: or(Superb_Estate_SE);
			},
			Three_spoke_multifunctional_leather_steering_wheel_with_radio_phone_and_DSG {
				ifin: or(Superb_Estate_SE, Superb_Estate_Elegance);
			},
			Three_spoke_multifunctional_leather_steering_wheel_with_control_module_for_radio_and_telephone {
				ifin: or(Fabia_Hatch_vRS);
			},
			Four_spoke_leather_steering_wheel_and_smaller_leather_pack {
				ifin: or(Fabia_Hatch_S, Fabia_Hatch_GreenLine_II,
					Superb_Estate_S);
			},
			Four_spoke_MFSW_for_radio_and_small_leather_pack {
				ifin: or(Fabia_Hatch_GreenLine_II);
			},
			Four_spoke_MFSW_for_telephone_and_radio_and_small_leather_pack {
				ifin: or(Fabia_Hatch_GreenLine_II);
			},
			Four_spoke_multifunctional_steering_wheel_with_radio_and_phone_controls_small_leather_package {
				ifin: or(Superb_Estate_S, Superb_Estate_SE);
			},
			Four_spoke_multifunctional_steering_wheel_with_radio_controls_small_leather_package {
				ifin: or(Superb_Estate_S);
				ifin: Maxi_dot_trip_computer;
			}
		} // other constraints
			!Superb_Estate_Elegance -> (
				Telephone_preparation && (Three_spoke_MFSW_for_radio_and_telephone_and_small_leather_pack ||
                   Three_spoke_multifunctional_leather_steering_wheel_with_radio_phone_and_DSG ||
				Three_spoke_multifunctional_leather_steering_wheel_with_control_module_for_radio_and_telephone ||
				Four_spoke_MFSW_for_telephone_and_radio_and_small_leather_pack ||
				Four_spoke_multifunctional_steering_wheel_with_radio_and_phone_controls_small_leather_package
			));
		},
		Floor_mats {
			ifin: or(Fabia_Hatch_vRS);
		},
		Front_armrest {
			ifin: or(Fabia_Hatch_SE, Fabia_Hatch_SE_Plus, Fabia_Hatch_Monte_Carlo, Fabia_Hatch_vRS);
		},
		Heated_seats group [1..1] {
			Heated_front_seats {
				ifin: or(Fabia_Hatch,
					Superb_Estate && ! Superb_Estate_Elegance);
			},
			Heated_front_and_rear_seats {
				ifin: or(Superb_Estate);
			}
		},
		Manually_adjustable_lumbar_supports {
			ifin: or(Superb_Estate_S);
		},
		Textile_floor_mats {
			ifin: or(Fabia_Hatch_S, Fabia_Hatch_GreenLine_II, Fabia_Hatch_SE, Fabia_Hatch_Monte_Carlo);
		},
		Third_rear_headrest {
			ifin: or(Fabia_Hatch, Yeti_E, Yeti_S);
		},
		Wood_decor {
			ifin: or(Superb_Estate_SE, Superb_Estate_SE_Plus, Superb_Estate_Elegance);
		}
	},
	Opt40_Seats_Seat_Upholstery group [0..*] {
		Monte_Carlo_Red_upholstery {
			ifin: or(Fabia_Hatch_Monte_Carlo);
			ifin: ColorInt_MonteCarlo_red;
		},
		PGC {
			ifin: or(Fabia_Hatch_Monte_Carlo);
			ifin: or(Three_spoke_MFSW_for_radio_and_small_leather_pack, Three_spoke_MFSW_for_radio_and_telephone_and_small_leather_pack);
		},
		Partial_leather_upholstery {
			ifin: or(Fabia_Hatch_SE, Fabia_Hatch_Elegance);
			ifin: (ColorInt_special_SE && Steering_wheel) || Fabia_Hatch_Elegance;
		},
		Leather_upholstery {
			ifin: or(Superb_Estate_SE);
			ifin: ColorInt_special_SE;
		},
		Ventilated_front_seats {
			ifin: or(Superb_Estate_Elegance);
		}
	},
	Opt50_Wheel_tire group [0..1] {
		Wheel_15inch_Antares_alloys_6Jx15_195_55_R15_85H {
			ifin: or(Fabia_Hatch_S);
		},
		Wheel_16inch_Atria_alloys_6_5Jx16_205_45_R16_83W {
			ifin: or(Fabia_Hatch_SE, Fabia_Hatch_SE_Plus);
		},
		Wheel_16inch_Elba_alloy_wheels {
			ifin: or(Fabia_Hatch_SE, Fabia_Hatch_SE_Plus, Fabia_Hatch_Elegance);
		},
		Wheel_17inch_Laurel_alloys_7_5Jx17_225_45_17_94W_Y {
			ifin: or(Superb_Estate_S, Superb_Estate_SE, Superb_Estate_SE_Plus);
		},
		Wheel_17inch_Trifid_alloys_7_5Jx17_225_45_R17_94W_Y {
			ifin: or(Superb_Estate_S);
		},
		Wheel_17inch_Dolomite_alloys_7Jx17_225_50_R17_94W {
			ifin: or(Yeti_S);
		},
		Wheel_18inch_Luxon_7_5Jx18_225_40_R18_92Y {
			ifin: or(Superb_Estate_SE, Superb_Estate_SE_Plus, Superb_Estate_Elegance);
		},
		Gigaro_Black_alloy_wheels_7Jx17_205_40_R17_7Jx17_ET41 {
			ifin: or(Fabia_Hatch_vRS);
		},
		Gigaro_Dark_Chrome_alloy_wheels_7Jx17_205_40_R17_7Jx17_ET41 {
			ifin: or(Fabia_Hatch_vRS);
		},
		Gigaro_White_alloy_wheels_7Jx17_205_40_R17_7Jx17_ET41 {
			ifin: or(Fabia_Hatch_vRS);
		}
	},
	Opt55_Spare_wheel group [0..1] {
		Temporary_steel_spare_wheel {
			ifin: or(Fabia_Hatch_S, Fabia_Hatch_SE, Fabia_Hatch_SE_Plus);
			ifin: ! or(Wheel_16inch_Atria_alloys_6_5Jx16_205_45_R16_83W,
				Wheel_16inch_Elba_alloy_wheels);
		},
		Temporary_space_saver_spare_wheel {
			ifin: or(Fabia_Hatch_SE, Fabia_Hatch_SE_Plus, Fabia_Hatch_Elegance, Fabia_Hatch_Monte_Carlo);
		},
		Space_saver_spare_steel_wheel {
			ifin: Superb_Estate;
			(Space_saver_spare_steel_wheel && Superb_Estate_S) -> (Wheel_17inch_Laurel_alloys_7_5Jx17_225_45_17_94W_Y || Wheel_17inch_Trifid_alloys_7_5Jx17_225_45_R17_94W_Y);
			(Wheel_17inch_Laurel_alloys_7_5Jx17_225_45_17_94W_Y || Wheel_17inch_Trifid_alloys_7_5Jx17_225_45_R17_94W_Y) -> (Space_saver_spare_steel_wheel && Superb_Estate_S);
		},
		Full_size_spare_wheel_car_jack_wheel_wrench {
			ifin: Superb_Estate_S;
		},
		Temporary_space_saver_spare_wheel_with_car_jack_and_false_boot_floor {
			ifin: Yeti_S;
		}
	},
	Opt60_Comfort group [0..*] {
		Parking_assistant group [1..1] {
			Acoustic_rear_parking_sensors {
				ifin: or(Fabia_Hatch && ! Fabia_Hatch_S);
			},
			Park_assist_with_front_and_rear_sensors {
				ifin: or(Superb_Estate_SE, Superb_Estate_SE_Plus, Superb_Estate_Elegance);
			},
			Park_distance_control {
				ifin: Superb_Estate;
			}
		},
		Auto_dimming_read_view_mirror {
			ifin: or(Superb_Estate_SE, Superb_Estate_SE_Plus, Superb_Estate_Elegance);
		},
		Black_protective_side_mouldings {
			ifin: or(Fabia_Hatch_S, Fabia_Hatch_GreenLine_II, Fabia_Hatch_SE, Fabia_Hatch_Monte_Carlo);
		},
		Climate_control_air_conditioning {
			ifin: or(Fabia_Hatch_GreenLine_II, Fabia_Hatch_SE, Fabia_Hatch_Monte_Carlo, Fabia_Hatch_vRS);
		},
		Climate_dual_zone_air_conditioning {
			ifin: or(Superb_Estate_S);
		},
		Cruise_control {
			ifin: or(Fabia_Hatch_SE, Fabia_Hatch_SE_Plus, Fabia_Hatch_Monte_Carlo, Fabia_Hatch_vRS,
				Superb_Estate_S);
		},
		Headlight_washers group [1..1] {
			Integrated_headlight_washers {
				ifin: or(Fabia_Hatch && ! Fabia_Hatch_GreenLine_II);
			},
			Integrated_retractable_headlight_washers {
				ifin: or(Superb_Estate_S, Superb_Estate_SE, Superb_Estate_SE_Plus);
			}
		},
		Maxi_dot_trip_computer {
			ifin: or(Fabia_Hatch_GreenLine_II, Fabia_Hatch_SE, Fabia_Hatch_SE_Plus, Fabia_Hatch_Monte_Carlo,
				Superb_Estate_S, Yeti_S);
		},
		Rain_sensor {
			ifin: or(Superb_Estate_S, Superb_Estate_SE, Superb_Estate_SE_Plus);
		},
		Rear_electric_windows {
			ifin: or(Fabia_Hatch && ! Fabia_Hatch_S);
		},
		Manual_air_conditioning {
			ifin: or(Fabia_Hatch_S);
		},
		Smoker_s_pack {
			ifin: or(Fabia_Hatch);
		},
		Sunroof group [1..1] {
			Electric_sunroof {
				ifin: or(Fabia_Hatch_SE, Fabia_Hatch_SE_Plus, Fabia_Hatch_Elegance, Fabia_Hatch_Monte_Carlo, Fabia_Hatch_vRS);
			},
			Paronamic_sunroof {
				ifin: or(Superb_Estate_SE, Superb_Estate_SE_Plus, Superb_Estate_Elegance);
			}
		},
		Sunset_glass {
			ifin: or(Fabia_Hatch_S, Fabia_Hatch_GreenLine_II, Fabia_Hatch_SE, Fabia_Hatch_Elegance,
				Superb_Estate_S, Superb_Estate_SE, Superb_Estate_Elegance);
		},
		Heated_windscreen {
			ifin: or(Superb_Estate_SE, Superb_Estate_SE_Plus, Superb_Estate_Elegance);
		}
	},
	Opt80_Radio_telephone_navigation { group [0..*] {
		Satellite_navigation group [1..1] {
			Amundsen_satellite_navigation_system_and_Maxi_dot {
				ifin: or(Fabia_Hatch && ! Fabia_Hatch_S);
			},
			Columbus_Satellite_Navigation {
				ifin: or(Superb_Estate_SE);
			}
		},
		Radio group [1..1] {
			Bolero_2_DIN_radio_with_integrated_CD_changer {
				ifin: or(Superb_Estate_S);
			},
			Six_CD_autochanger {
				ifin: or(Superb_Estate_S && Boot_floor,
					Superb_Estate_SE && Columbus_Satellite_Navigation,
					Superb_Estate_SE_Plus,
					Superb_Estate_Elegance,
					Yeti_S);
			}
		},
		Integrated_data_storage_device_for_Western_Europe,
		MDI {
			ifin: or(Fabia_Hatch_SE, Fabia_Hatch_Elegance,
				Superb_Estate_S, Superb_Estate_SE, Superb_Estate_Elegance);
		},
		Ten_loudspeakers {
			ifin: or(Superb_Estate_SE, Superb_Estate_SE_Plus, Superb_Estate_Elegance);
		},
		Telephone_preparation group [1..1] {
			Telephone_preparation_GSM_II_with_Bluetooth {
				ifin: or(Fabia_Hatch && ! Fabia_Hatch_S);
			},
			Telephone_preparation_with_Bluetooth_GSM_III {
				ifin: or(Superb_Estate_S, Superb_Estate_SE);
			}
		}
	} // other constraints
		Satellite_navigation -> Integrated_data_storage_device_for_Western_Europe;
		 Integrated_data_storage_device_for_Western_Europe -> Satellite_navigation;
		! (Telephone_preparation && Maxi_dot_trip_computer);
	},
	Opt90_Vehicle_security group [0..*] {
		Alarm_with_interrior_monitoring {
			ifin: or(Fabia_Hatch_S);
		},
		KESSY {
			ifin: or(Superb_Estate_S, Superb_Estate_SE, Superb_Estate_SE_Plus);
		}
	},
	Opt100_Rest { group [0..*] {
		Exterior_mirror_housings_painted {
			ifin: or(Fabia_Hatch_SE, Fabia_Hatch_SE_Plus, Fabia_Hatch_Elegance);
		},
		Coloured_roof {
			ifin: or(Fabia_Hatch_vRS);
		}
	} // other constraints
		Exterior_mirror_housings_painted -> (Metallic_colors_for_silver_roof || Solid_colors_for_silver_roof);
		(Metallic_colors_for_silver_roof || Solid_colors_for_silver_roof) -> Exterior_mirror_housings_painted ;
		Fabia_Hatch_vRS -> (Coloured_roof && (Metallic_colors_for_black_roof || Metallic_colors_for_white_roof || Solid_colors_for_black_roof || Solid_colors_for_white_roof));
	},
	Opt120_Special_packages group [0..*] {
		Left_hand_drive_for_EU_countries {
			ifin: or(Fabia_Hatch_SE);
			ifin: false; // TODO couldn't find the constraint
		}
	},
	Opt_Special_edition_packages group [0..*] {
		Package_L_K {
			this -> ColorInt_special_L_K;
			ColorInt_special_L_K -> this;
			
			this -> L_K_front_and_rear_floor_mats;
			L_K_front_and_rear_floor_mats -> this;
			ifin: ! or(Net_programme,
				Steering_wheel, Heated_seats, Wood_decor,
				Opt40_Seats_Seat_Upholstery,
				Opt50_Wheel_tire,
				Parking_assistant, Sunroof, Sunset_glass,
				Ten_loudspeakers);
		}
	},
	Opt_Warranty_prolongation group [0..1] {
		Four_year_or_80000_miles_extended_warranty,
		Five_year_or_100000_miles_extended_warranty
	}
}
